\ core.ok - basic definitions for okami
\ Copyright (C) 2018 Wolfgang Jaehrling
\
\ ISC License
\
\ Permission to use, copy, modify, and/or distribute this software for any
\ purpose with or without fee is hereby granted, provided that the above
\ copyright notice and this permission notice appear in all copies.
\
\ THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
\ WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
\ MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
\ ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
\ WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
\ ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
\ OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

create : docol , [create docol , exit]
: ; [lit exit , exit]

: here [hp @];

: ? [@ .];
: bye [0 sysexit];

: 2dup [over over];
: 2drop [drop drop];

: cell [4];
: cells [cell *];
: cell+ [cell +];
: cell- [cell -];

: 1+ [1 +];
: 1- [1 -];
: 0= [0 =];
: negate [0 swap -];

: true [-1];
: false [0];
: on [true swap !];
: off [false swap !];

: ' [word find];
: alias [here ' hp ! create hp !];

: cr [10 emit];
: space [32 emit];

\ helpers for defining control structures
: from-here [here 0 ,];
: to-here [here swap !];

: if [[0branch] from-here];
: then [to-here];
: else [[branch] from-here >r to-here r>];

: begin [here];
: while [[0branch] from-here];
: repeat [[branch] swap , to-here];

: abs [dup 0 <] if [negate] then ;  \ TODO: simplify with <?

: max [2dup >] if [drop] else [nip] then ;
: min [2dup <] if [drop] else [nip] then ;

: with [[create dodoes , lit] from-here [,]];
: does [[exit] to-here];

: const with [,] does [@];
: var with [0 ,] does [];
: array with [cells allot drop] does [swap cells +];

: struct{ [0];
: field with [over , +] does [@ +];
: }struct with [,] does [@ allot const];
